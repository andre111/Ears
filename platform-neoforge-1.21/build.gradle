plugins {
	id 'java-library'
    id 'eclipse'
    id 'net.neoforged.gradle.userdev' version '7.0.145'
    id 'net.neoforged.gradle.common' version '7.0.145'
}

repositories {
	maven { url 'https://repo.spongepowered.org/maven/' }
}

version = file('../version.txt').text.trim()+file('version-suffix.txt').text.trim()
group = 'com.unascribed.ears'
base.archivesName = 'ears-neoforge-1.21'

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

jarJar.enable()

dependencies {
	implementation "net.neoforged:neoforge:${project.neo_version}"
	implementation project(':common')
	jarJar project(':common')
}

processResources {
    var expandProps = [
            "version": project.version,
            "loader_version": project.loader_version
    ]
    inputs.properties(expandProps)
    filesMatching("META-INF/neoforge.mods.toml") {
        expand expandProps
    }
}

tasks.named('jarJar') {
    archiveClassifier = ''
}

tasks.named('jar') {
    archiveClassifier = 'slim'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}


runs {
	configureEach {
	    modSource sourceSets.main
	    dependencies {
	    	runtime project(':common')
	    }
	}
	
    client {
    	devLogin {
            enabled true
        }
    }
}

jar {
    from "../LICENSE"
}
